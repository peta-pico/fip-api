#+ summary: Get list of resources that occur in FIP declarations
#+ method: GET
#+ pagination: 1000
#+ endpoint: https://query.knowledgepixels.com/repo/full
#+ endpoint_in_url: False

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix fip: <https://w3id.org/fair/fip/terms/>
prefix dct: <http://purl.org/dc/terms/>
prefix dce: <http://purl.org/dc/elements/1.1/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix np: <http://www.nanopub.org/nschema#>

prefix showdeclarations: <https://tapas.knowledgepixels.com/tapas.html?api=peta-pico/fip-api&op=/get-declarations&autosubmit=on&param_resource=>

select distinct ?resource ?resourceLabel ?resourcetype (count(distinct ?community) as ?community_count) ?show_declarations ?show_declarationsLabel where {
  graph npa:graph {
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
    ?np np:hasAssertion ?assertion .
    ?np dct:created ?date .
    ?np npx:hasNanopubType fip:FIP-Declaration .
  }
  graph ?assertion {
    ?decl a fip:FIP-Declaration .
    ?decl fip:refers-to-question ?question .
    ?decl fip:declared-by ?__community_iri .
    values ?rel {
      fip:declares-current-use-of fip:declares-planned-use-of fip:declares-planned-replacement-of fip:declares-planned-development-of
      # unofficial:
      fip:declares-replacement-from fip:declares-replacement-to
    }
    ?decl ?rel ?resource .
    filter(strstarts(str(?question), str(fip:)))
  }
  bind(?__community_iri as ?community)
  ?__community_iri fip:has-research-domain ?__domain_iri .
  bind(?__domain_iri as ?domain)
  optional { ?resource rdfs:label ?resourceLabel . }
  optional {
    values ?resourcetype { fip:Available-FAIR-Enabling-Resource fip:FAIR-Enabling-Resource-to-be-Developed }
    ?resource a ?resourcetype
  }

  bind(uri(concat(str(showdeclarations:), encode_for_uri(str(?resource)))) as ?show_declarations)
  bind('show declarations' as ?show_declarationsLabel)
}
group by ?resource ?resourceLabel ?resourcetype ?show_declarations ?show_declarationsLabel
order by ?resourceLabel
