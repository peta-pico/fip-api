#+ summary: Get list of FAIR Implementation Profile declarations
#+ method: GET
#+ pagination: 1000
#+ endpoint: https://query.knowledgepixels.com/repo/full
#+ endpoint_in_url: False

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix fip: <https://w3id.org/fair/fip/terms/>
prefix dct: <http://purl.org/dc/terms/>
prefix dce: <http://purl.org/dc/elements/1.1/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix np: <http://www.nanopub.org/nschema#>

select distinct ?community ?communityLabel ?question ?questionLabel ?sort ?resource ?resourceLabel ?rel ?resourcetype ?np ?npLabel where {
  graph npa:graph {
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
    ?np np:hasAssertion ?assertion .
    ?np dct:created ?date .
    ?np npx:hasNanopubType fip:FIP-Declaration .
  }
  graph ?assertion {
    ?decl a fip:FIP-Declaration .
    ?decl fip:refers-to-question ?question .
    ?decl fip:declared-by ?__community_iri .
    values ?rel {
      fip:declares-current-use-of fip:declares-planned-use-of fip:declares-planned-replacement-of
      # unofficial:
      fip:declares-replacement-from fip:declares-replacement-to
    }
    ?decl ?rel ?__resource_iri .
  }
  ?__community_iri fip:has-research-domain ?__domain_iri .
  bind(replace(str(?np), '^.*(RA[a-zA-Z0-9-_]{4})[a-zA-Z0-9-_]{39}$', '$1') as ?npLabel)
  optional { ?__resource_iri rdfs:label ?resourceLabel }
  optional {
    values ?resourcetype { fip:Available-FAIR-Enabling-Resource fip:FAIR-Enabling-Resource-to-be-Developed }
    ?__resource_iri a ?resourcetype
  }
  bind(?__community_iri as ?community)
  bind(replace(str(?community), ".*#", "") as ?communityLabel)
  bind(?__resource_iri as ?resource)
  bind(replace(str(?question), "^.*-([^-MD]+(-[MD]+)?)$", "$1") as ?questionLabel)
  bind(concat(replace(?questionLabel, "F|M", "0"), "x") as ?sort)
} order by ?sort ?communityLabel
