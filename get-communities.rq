#+ summary: Get list of FAIR Implementation Communities
#+ method: GET
#+ pagination: 1000
#+ endpoint: https://query.knowledgepixels.com/repo/full
#+ endpoint_in_url: False

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix fip: <https://w3id.org/fair/fip/terms/>

prefix showdeclarations: <https://tapas.knowledgepixels.com/tapas.html?api=peta-pico/fip-api&op=/get-declarations&autosubmit=on&param_community=>
prefix showresources: <https://tapas.knowledgepixels.com/tapas.html?api=peta-pico/fip-api&op=/get-resources&autosubmit=on&param_community=>

select distinct ?community ?communityLabel ?name ?np ?npLabel ?date ?show_declarations ?show_declarationsLabel ?show_resources ?show_resourcesLabel where {
  graph npa:graph {
    ?np np:hasAssertion ?a .
    ?np np:hasPublicationInfo ?i.
    ?np dct:created ?date .
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
  }
  graph ?a {
    ?community a fip:FAIR-Implementation-Community .
    ?community fip:has-research-domain ?__domain_iri .
    ?community rdfs:label ?name .
  }
  
  bind(replace(str(?np), '^.*(RA[a-zA-Z0-9-_]{4})[a-zA-Z0-9-_]{39}$', '$1') as ?npLabel)
  bind(replace(str(?community), '^.*[#/]([^#/]+)$', '$1') as ?communityLabel)

  bind(uri(concat(str(showdeclarations:), encode_for_uri(str(?community)))) as ?show_declarations)
  bind('show declarations' as ?show_declarationsLabel)
  bind(uri(concat(str(showresources:), encode_for_uri(str(?community)))) as ?show_resources)
  bind('show resources' as ?show_resourcesLabel)
}
order by asc(?name)
