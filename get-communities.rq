#+ summary: Get list of FAIR Implementation Communities
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix fip: <https://w3id.org/fair/fip/terms/>

prefix showdeclarations: <?api=peta-pico/fip-api&op=/get-declarations&autosubmit=on&param_community=>

select distinct ?community ?communityLabel ?name ?np ?npLabel ?date ?show_declarations ?show_declarationsLabel where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np dct:created ?date .
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
  }
  graph ?h {
    ?np np:hasAssertion ?a .
    ?np np:hasPublicationInfo ?i.
  }
  graph ?a {
    ?community a fip:FAIR-Implementation-Community .
    ?community fip:has-research-domain ?__domain_iri .
    ?community rdfs:label ?name .
  }
  
  bind(replace(str(?np), '^.*(RA[a-zA-Z0-9-_]{4})[a-zA-Z0-9-_]{39}$', '$1') as ?npLabel)
  bind(replace(str(?community), '^.*[#/]([^#/]+)$', '$1') as ?communityLabel)

  filter not exists {
    graph npa:graph {
      ?newversion npa:hasHeadGraph ?nh .
      ?newversion npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?nh {
      ?newversion np:hasPublicationInfo ?ni .
    }
    graph ?ni {
      ?newversion npx:supersedes ?np .
    }
  }
  filter not exists {
    graph npa:graph {
      ?retraction npa:hasHeadGraph ?rh .
      ?retraction npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?np .
    }
  }

  bind(uri(concat(showdeclarations:, encode_for_uri(?community))) as ?show_declarations)
  bind('show declarations' as ?show_declarationsLabel)
}
order by asc(?name)
