#+ summary: Get list of domains
#+ method: GET
#+ pagination: 1000
#+ endpoint: https://query.knowledgepixels.com/repo/full
#+ endpoint_in_url: False

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix fip: <https://w3id.org/fair/fip/terms/>

prefix showcommunities: <https://tapas.knowledgepixels.com/tapas.html?api=peta-pico/fip-api&op=/get-communities&autosubmit=on&param_domain=>
prefix showresources: <https://tapas.knowledgepixels.com/tapas.html?api=peta-pico/fip-api&op=/get-resources&autosubmit=on&param_domain=>

select distinct ?domain ?domainLabel (count(distinct ?community) as ?community_count) ?show_communities ?show_communitiesLabel ?show_resources ?show_resourcesLabel where {
  graph npa:graph {
    ?np np:hasAssertion ?a .
    ?np np:hasPublicationInfo ?i.
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
  }
  graph ?a {
    ?community a fip:FAIR-Implementation-Community .
    ?community fip:has-research-domain ?domain .
  }
  ?domain rdfs:label ?domainLabel .

  bind(replace(str(?community), '^.*[#/]([^#/]+)$', '$1') as ?communityLabel)

  bind(uri(concat(str(showcommunities:), encode_for_uri(str(?domain)))) as ?show_communities)
  bind('show communities' as ?show_communitiesLabel)
  bind(uri(concat(str(showresources:), encode_for_uri(str(?domain)))) as ?show_resources)
  bind('show resources' as ?show_resourcesLabel)
}
group by ?domain ?domainLabel ?show_communities ?show_communitiesLabel ?show_resources ?show_resourcesLabel
order by asc(?domainLabel)
